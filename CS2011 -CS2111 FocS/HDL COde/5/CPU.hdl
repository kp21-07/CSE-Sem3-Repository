/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         -- M value input  (M = contents of RAM[A])
        instruction[16], -- Instruction for execution
        reset;           -- Signals whether to re-start the current
                         -- program (reset==1) or continue executing
                         -- the current program (reset==0).

    OUT outM[16],        -- M value output
        writeM,          -- Write to M? 
        addressM[15],    -- Address in data memory (of M)
        pc[15];          -- address of next instruction

    PARTS:
		Not(in=instruction[15], out=isAInstruction);
		Mux(a=false, b=true, sel=instruction[15], out=isCInstruction);

		Mux16(a=instruction, b=aluOut, sel=isCInstruction, out=inAReg);

		And(a=isCInstruction, b=instruction[5], out=loadAfromC);
		Or(a=isAInstruction, b=loadAfromC, out=loadA);
		ARegister(in=inAReg, load=loadA, out=aOut, out[0..14]=addressM);

		And(a=isCInstruction, b=instruction[4], out=loadD);
		DRegister(in=aluOut, load=loadD, out=dOut);

		Mux16(a=aOut, b=inM, sel=instruction[12], out=aluYin);

		ALU(x=dOut, y=aluYin,
				zx=instruction[11], nx=instruction[10],
				zy=instruction[9], ny=instruction[8],
				f=instruction[7], no=instruction[6],
				out=aluOut, out=outM, zr=zrOut, ng=ngOut);

		And(a=isCInstruction, b=instruction[3], out=writeM);
		
		Not(in=ngOut, out=notNG);
		Not(in=zrOut, out=notZR);
		And(a=notNG, b=notZR, out=isPos);

		And(a=instruction[0], b=isPos, out=doJGT);
		And(a=instruction[1], b=zrOut, out=doJEQ);
		And(a=instruction[2], b=ngOut, out=doJLT);

		Or(a=doJGT, b=doJEQ, out=JGE);
		Or(a=JGE, b=doJLT, out=JMP);

		And(a=isCInstruction, b=JMP, out=pcLoad);

		PC(in=aOut, load=pcLoad, inc=true, reset=reset, out[0..14]=pc);
		}
